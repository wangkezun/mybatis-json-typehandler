buildscript {
    ext {
        kotlin_version = '1.3.50'
        scm_url = 'https://github.com/wangkezun/mybatis-json-typehandler'
        scm_connection = 'scm:git:git@github.com:wangkezun/mybatis-json-typehandler.git'
        scm_developerConnection = 'scm:git:git@github.com:wangkezun/mybatis-json-typehandler.git'
        scm_tag = 'HEAD'
        issueUrl = 'https://github.com/wangkezun/mybatis-json-typehandler/issues'
    }

    repositories {
        mavenCentral()
        jcenter()
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.50"
    id 'jacoco'
    id 'maven-publish'
    id 'maven'
    id 'signing'
    id 'org.jetbrains.dokka' version "0.10.0"
}


group = 'io.wkz.kotlin'
version = '1.0.2'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile group: 'org.mybatis', name: 'mybatis', version: '3.4.6'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.7'
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.9.7'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.1'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.0.6.RELEASE'
    testCompile group: 'mysql', name: 'mysql-connector-java', version: '8.0.13'
    testCompile group: 'org.mybatis.spring.boot', name: 'mybatis-spring-boot-starter', version: '1.3.2'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.1'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
test {
    useJUnitPlatform()
}

// build.gradle
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

check.dependsOn jacocoTestReport

dokka {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
    inputs.dir 'src/main/kotlin'
    cacheRoot = 'default'
}

task packageJavadoc(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'mybatis-json-typehandler'
                url = 'https://github.com/wangkezun/mybatis-json-typehandler'
                description = 'A Mybatis TypeHandler for json object and json array'
                scm {
                    url = scm_url
                    connection = scm_connection
                    developerConnection = scm_developerConnection
                    tag = scm_tag
                }

                license {
                    name = 'MIT Licence'
                    url = 'https://raw.githubusercontent.com/wangkezun/mybatis-json-typehandler/master/LICENSE'
                    distribution = 'repo'
                }
                developers {
                    developer {
                        id= 'wangkezun'
                        name = 'Wang Kezun'
                        email = 'wangkezun@gmail.com'
                    }
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = issueUrl
                }
            }
            artifact sourceJar {
                classifier "sources"
            }
            artifact packageJavadoc {
                classifier "javadoc"
            }
        }
    }
    repositories {
        maven {
            if (project.version.endsWith('-SNAPSHOT')) {
                url = "https://oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}
signing {
    required { gradle.taskGraph.hasTask("publishing") }
    sign publishing.publications.mavenJava
}
